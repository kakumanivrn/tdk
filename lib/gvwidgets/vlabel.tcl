# Copyright (c) 2018 ActiveState Software Inc.
# Released under the BSD-3 license. See LICENSE file for details.
#
# -*- tcl -*-
# ### ######### ###########################

# UI tools. Widget. Label connected connected to a view (row) as
# generated by 'view' (See tools), or anything following the same
# interface, oomk/metakit for example. Has a 'clabel' inside.

# ### ######### ###########################
## Prerequisites

package require snit    ; # Object system
package require clabel  ; # Our fundament

# ### ######### ###########################
## Implementation

snit::widgetadaptor vlabel {
    # ### ######### ###########################

    delegate method * to hull
    delegate option * to hull
    # FUTURE: Exclude -text, -textvariable, -cursor

    option -view {} ; # Reference to the view displayed by the widget.
    option -row  {}

    # ### ######### ###########################
    ## Public API. Construction.

    constructor {args} {
	installhull [clabel $win]
	$self configurelist $args
	return
    }

    # ### ######### ###########################
    ## Public API. Row manipulation (go forward/backward).

    method next {} {
	incr               row
	set options(-row) $row
	set cursor(#)     $row ; # Trigger refresh of display
	return
    }

    method previous {} {
	incr               row -1
	set options(-row) $row
	set cursor(#)     $row ; # Trigger refresh of display
	return
    }

    # ### ######### ###########################
    ## Internal. State of widget.

    variable cursor   ; # The cursor given to our hull
    variable row    0 ; # The row currently shown

    # ### ######### ###########################
    ## Internal. Handle changes to the options.

    onconfigure -view {view} {
	if {$options(-view) eq $view} return

	if {$options(-view) != {}} {
	    $options(-view) removeOnChangeCall $self
	    $hull configure -cursor {}
	    unset     cursor
	    array set cursor {}
	}

	set options(-view) $view

	if {$view != {}} {
	    $view cursor cursor
	    $hull configure -cursor [varname cursor]
	    $view onChangeCall $self
	    set cursor(#) $row ; # Trigger display
	}
	return
    }

    onconfigure -row {r} {
	if {$options(-row) eq $r} return

	set options(-row) $r
	set row           $r
	set cursor(#)     $r
	return
    }

    # ### ######### ###########################
    ## Internal. Change propagation. Called by the view we are
    ## attached to when its contents have changed.

    method change {o} {
	set cursor(#) $row ; # Trigger display refresh
	return
    }

    # ### ######### ###########################
    ## Block the options we use internally ...
    ## They are effectively nulled out.

    foreach o {-text -textvariable -cursor} {
	option $o {}
	onconfigure $o {args} "#return -code error \"Unknown option $o\""
	oncget      $o        "#return -code error \"Unknown option $o\""
    }
    unset o

    # ### ######### ###########################
}

# ### ######### ###########################
## Ready for use

package provide vlabel 0.1
